apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.lukekorth.photo_paper"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 11
        versionName "0.4.4"
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            storeFile file(System.properties['KEYSTORE'])
            storePassword ""
            keyAlias System.properties['KEYSTORE_EMAIL']
            keyPassword ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

android.buildTypes.each { type ->
    type.buildConfigField 'String', 'CONSUMER_KEY', System.properties['CONSUMER_KEY'] ?: '""'
    type.buildConfigField 'String', 'CONSUMER_SECRET', System.properties['CONSUMER_SECRET'] ?: '""'
}

dependencies {
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:palette-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'

    compile 'com.google.firebase:firebase-core:9.2.0'
    compile 'com.google.firebase:firebase-crash:9.2.0'

    compile 'se.akerfeldt:okhttp-signpost:1.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.squareup:otto:1.3.8'

    compile 'it.sephiroth.android.library.imagezoom:imagezoom:1.0.5'
    compile 'com.lukekorth:mailable_log:0.1.3'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0.3@aar'

    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.uphyca:stetho_realm:0.9.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
}

// borrowed from https://www.timroes.de/2014/01/19/using-password-prompts-with-gradle-build-files/
// and https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
gradle.taskGraph.whenReady { taskGraph ->
    // Only execute when we are trying to assemble a release build
    if (taskGraph.hasTask(':PhotoPaper:assembleRelease') || taskGraph.hasTask(':PhotoPaper:installRelease')) {
        def password = System.console().readPassword("\nPlease enter key passphrase: ")
        if (password.size() <= 0) {
            throw new InvalidUserDataException("You must enter a password to proceed.")
        }

        // Must create String because System.readPassword() returns char[]
        // (and assigning that below fails silently)
        password = new String(password)

        android.signingConfigs.release.storePassword = password
        android.signingConfigs.release.keyPassword = password
    }
}

apply plugin: 'com.google.gms.google-services'
